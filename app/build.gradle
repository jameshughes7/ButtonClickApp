apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.jameshughes.buttonclickapp"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 22
        versionName "1.0"
        testInstrumentationRunner getInstrumentation()
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'TEST_TAGS', '"'+getTestTags()+'"'
            buildConfigField 'String', 'TEST_SCENARIO', '"'+getTestScenario()+'"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//Ternary statement deciding which runner will be used against the emulator
def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.example.jameshughes.buttonclickapp.cucumber.runner.CucumberTestRunner' : 'android.support.test.runner.AndroidJUnitRunner'
}

def getTestTags() {
    project.getProperties().get('tags') ?: ''
}

def getTestScenario() {
    project.getProperties().get('scenario') ?: ''
}

dependencies {
    def appCenterSdkVersion = '1.11.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    //Espresso & JUnit
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    //Cucumber
    androidTestImplementation 'info.cukes:cucumber-android:1.2.5'
    androidTestImplementation 'info.cukes:cucumber-picocontainer:1.2.5'
}
